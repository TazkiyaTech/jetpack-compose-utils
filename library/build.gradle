plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.jetbrains.kotlin.android)
    id("maven-publish")
    id("signing")
}

android {
    namespace = "com.tazkiyatech.compose.utils"
    compileSdk = 34

    defaultConfig {
        minSdk = 21
        targetSdk = 34
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled = false
            shrinkResources = false
        }
        release {
            minifyEnabled = false
            shrinkResources = false
        }
    }

    buildFeatures {
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = libs.versions.compose.compiler.get()
    }

    compileOptions {
        sourceCompatibility(JavaVersion.VERSION_17)
        targetCompatibility(JavaVersion.VERSION_17)
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    lint {
        abortOnError = true
    }

    publishing {
        singleVariant("release") {
            withJavadocJar()
            withSourcesJar()
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

dependencies {
    // NOTE: we're adding a test library as a non-test dependency because that's a requirement of the library
    debugImplementation(libs.compose.ui.testmanifest)
    debugImplementation(libs.compose.ui.tooling)

    implementation(platform(libs.compose.bom))

    implementation(libs.compose.material3)
    implementation(libs.compose.ui.ui)
    implementation(libs.compose.ui.graphics)
    implementation(libs.compose.ui.toolingpreview)
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId getProperty("projectGroupName")
                artifactId getProperty("projectArtifactName")
                version getProperty("projectVersionName")

                pom {
                    name = getProperty("projectArtifactName")
                    description = "An Android library containing views for Jetpack Compose."
                    url = "https://github.com/TazkiyaTech/jetpack-compose-utils"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    organization {
                        name = "Tazkiya Tech"
                        url = "http://tazkiyatech.com"
                    }
                    developers {
                        developer {
                            id = "adil-hussain-84"
                            name = "Adil Hussain"
                            email = "adilson05uk@gmail.com"
                        }
                    }
                    scm {
                        connection = "scm:git:git://github.com/TazkiyaTech/jetpack-compose-utils.git"
                        developerConnection = "scm:git:ssh://github.com:TazkiyaTech/jetpack-compose-utils.git"
                        url = "https://github.com/TazkiyaTech/jetpack-compose-utils"
                    }
                }
            }
        }

        repositories {
            maven {
                url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username getProperty("sonatype_username")
                    password getProperty("sonatype_password")
                }
            }
        }
    }

    signing {
        // the 'signing.keyId', 'signing.password' and 'signing.secretKeyRingFile' properties required by this task are defined outside of this project in the '~/.gradle/gradle.properties' file
        sign publishing.publications.release
    }
}
